$include "std/memory.tome"
$include "std/io.tome"


inline :list-element-size  is  0 + read ;
inline :list-element-size! is  0 + write ;

inline :list-capacity      is  8 + read ;
inline :list-capacity!     is  8 + write ;

inline :list-count         is 16 + read ;
inline :list-count!        is 16 + write ;

inline :list-ptr           is 24 + read ;
inline :list-ptr!          is 24 + write ;

inline sizeof(List)  is 32 ;

inline max is -> a, b do
    if a b < then b else a ;;;

inline init-list is -> ptr, elem-size do

    -- Set all of the list's values
    elem-size   ptr :list-element-size!
    0           ptr :list-capacity!
    0           ptr :list-count!
    0           ptr :list-ptr!

    -- Return the pointer to the list
    ptr ;;

def append is -> list do
    if list :list-count 1 +
       list :list-element-size *
       list :list-capacity >
    then
        -- capacity = min(8 els, 2 * capacity)
        8 list :list-element-size *
        2 list :list-capacity     *
        max dup list :list-capacity!

        list :list-ptr re-allocate
        list :list-ptr! ;

    -- elem = list.ptr[list.count]
    list :list-ptr list :list-count list :list-element-size * + -> elem do

    -- list.count = list.count + 1
    list :list-count 1 + list :list-count!

    -- return elem
    elem ;;;

inline get is -> list, n do
    list :list-ptr list :list-element-size n * + ;;

inline new-list is
    sizeof(List) allocate swap init-list ;

inline delete-list is -> list do
    list :list-ptr free
    list free ;;
